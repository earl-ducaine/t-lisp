(HERALD (TSYS VMSYSTEM T 19)
        (ENV TSYS))

;;; Copyright (c) 1983, 1984 Yale University

;;; General embedded system stuff.

(DEFINE-OPERATION (SYSTEM-NAME SYSTEM) NIL)
(DEFINE-OPERATION (INITIALIZE-SYSTEM SYSTEM) NIL)
(DEFINE-OPERATION (REINITIALIZE-SYSTEM SYSTEM) NIL)

(DEFINE-OPERATION (SET-SYSTEM-GENERATION-NUMBER SYSTEM N) NIL)
(DEFINE SYSTEM-GENERATION-NUMBER
  (OPERATION (LAMBDA (SYSTEM) 0)
    ((SETTER SELF) SET-SYSTEM-GENERATION-NUMBER)))

(DEFINE-OPERATION (PRINT-SYSTEM-HERALD SYSTEM)
  (PRINT-SYSTEM-HERALD *THE-T-SYSTEM*))

(DEFINE (INITIALIZE-SYSTEMS)
  (BIND ((*BREAK-LEVEL* 1))             ; Hack
    (LET ((SYSTEMS (COND ((NULL? (CDR *EMBEDDED-SYSTEMS*)) *EMBEDDED-SYSTEMS*)
                         (ELSE (REVERSE *EMBEDDED-SYSTEMS*))))) ; avoid dpdncy
      (DO ((S SYSTEMS (CDR S))
           (G *GENERATION-NUMBERS* (CDR G)))
          ((NULL? S)
           (PRINT-SYSTEM-HERALD (CAR *EMBEDDED-SYSTEMS*))
           (WALK1 INITIALIZE-SYSTEM SYSTEMS))
          (SET-SYSTEM-GENERATION-NUMBER (CAR S) (CAR G))))))

(DEFINE (REINITIALIZE-SYSTEMS)
  (BIND ((*BREAK-LEVEL* 1))             ; Hack
    (LET ((SYSTEMS (COND ((NULL? (CDR *EMBEDDED-SYSTEMS*)) *EMBEDDED-SYSTEMS*)
                         (ELSE (REVERSE *EMBEDDED-SYSTEMS*))))) ; avoid dpdncy
      (DO ((S SYSTEMS (CDR S))
           (G *GENERATION-NUMBERS* (CDR G)))
          ((NULL? S)
           (PRINT-SYSTEM-HERALD (CAR *EMBEDDED-SYSTEMS*))
           (WALK1 REINITIALIZE-SYSTEM SYSTEMS))
          (SET-SYSTEM-GENERATION-NUMBER (CAR S) (CAR G))))))

;;; First embedded system is VM system.
;;; List of embedded systems must correspond to list of generation numbers
;;; set up by bind script written by MAKE-SYSTEM.

(DEFINE *THE-VM-SYSTEM*
  (LET ((GEN 0))
    (OBJECT NIL
	    ((SET-SYSTEM-GENERATION-NUMBER SELF N) (SET GEN N))
	    ((SYSTEM-NAME SELF) 'VM)
	    ((PRINT-SYSTEM-HERALD SELF)
	     (ZFORMAT ZTERMINAL-OUTPUT "~%T virtual machine 1.0 (~S)~%" GEN))
	    ((IDENTIFICATION SELF) '*THE-VM-SYSTEM*))))

(PUSH *EMBEDDED-SYSTEMS* *THE-VM-SYSTEM*)

(SET *RE-ENTER-ON-FAULT?* T)

(SET *TOP-LEVEL*
     (LAMBDA ()
       (SET *TOP-LEVEL* ZBREAKPOINT)
       (INITIALIZE-SYSTEMS)))
